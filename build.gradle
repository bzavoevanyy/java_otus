plugins {
    // support Idea IDE
    id 'idea'
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    // Build uber-jar
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.5.5")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }

    task managedVersions {
        doLast {
            dependencyManagement.managedVersions.each {
                println it
            }
        }
    }
}