plugins {
    // support Idea IDE
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    // Build uber-jar
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.6.3")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
            dependency("org.testcontainers:junit-jupiter:${Versions.testcontainers}")
            dependency("org.testcontainers:postgresql:${Versions.testcontainers}")
            dependency("io.grpc:grpc-netty:${Versions.grpc}")
            dependency("io.grpc:grpc-protobuf:${Versions.grpc}")
            dependency("io.grpc:grpc-stub:${Versions.grpc}")
            dependency("com.google.protobuf:protobuf-java-util:${Versions.protobuf}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force("net.java.dev.jna:jna:5.8.0")
        }
    }

    task managedVersions {
        doLast {
            dependencyManagement.managedVersions.each {
                println it
            }
        }
    }
}